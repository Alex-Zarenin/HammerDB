# Copyright: 2016-2021 Paul Obermeier (obermeier@tcl3d.org)
# Distributed under BSD license.
#
# BuildType: MSys / gcc
#
# Possible user configuration options (--copt):
#   Any -DXXX option usable for CFLAGS environment variable.
#
# Changes to source code (manual additions):
# Versions 8.6.5 and 8.6.6 generic/tkImgPhoto.c:
# Added patch for photo commands "read", "put" and "write",
# which do not allow filenames or data starting with "-".
# Applied bug fix for Mac (http://core.tcl-lang.org/tk/info/6cac4ac67e845927)

proc Init_Tk { libName libVersion } {
    SetScriptAuthor    $libName "Paul Obermeier" "obermeier@tcl3d.org"
    SetLibHomepage     $libName "http://www.tcl-lang.org/"
    SetLibDependencies $libName "Tcl"
    SetPlatforms       $libName "All"
    SetWinCompilers    $libName "gcc"
}

proc Build_Tk { libName libVersion buildDir instDir devDir distDir } {
    set buildDirMSys     [MSysPath $buildDir]
    set instDirMSys      [MSysPath $instDir]
    set rootBuildDirMSys [MSysPath [GetOutputBuildDir]]

    if { [UseStage "Extract" $libName] } {
        ExtractLibrary $libName $buildDir
    }

    if { [UseStage "Configure" $libName] } {
        set cmd [GetUserCFlags $libName]
        if { [IsWindows] } {
            append cmd "$buildDirMSys/win/configure "
            append cmd "--build=[GetMingwVersion] "
        } else {
            append cmd "$buildDirMSys/unix/configure "
	    append cmd "--enable-xft "
	    append cmd "--enable-xss=no "
        }
        append cmd     "--prefix=$instDirMSys --exec-prefix=$instDirMSys "
        append cmd     "--with-tcl=$rootBuildDirMSys/Tcl "
        if { [Is64Bit] } {
            append cmd "--enable-64bit "
        }
        if { [IsDebugBuild] } {
            append cmd "--enable-symbols "
        } else {
            append cmd "--disable-symbols "
        }
        if { [IsDarwin] } {
            append cmd "--enable-aqua "
        }
        append cmd "--disable-zipfs "

        MSysRun $libName "${libName}_Configure" $buildDir "$cmd"
    }

    if { [UseStage "Compile" $libName] } {
        MSysBuild $libName $buildDir "install"
        # Install internal header files, too. Need for Togl widget.
        MSysBuild $libName $buildDir "install-private-headers"

        set tkLib [GetTkLibName $libVersion]
        if { [NeedDll2Lib $libName] } {
            Dll2Lib $libName "$instDir/bin" "${tkLib}.dll" "${tkLib}.def" "${tkLib}.lib"
        }

        SingleFileCopy "$instDir/bin/[GetWishName $libVersion]" "$instDir/bin" "wish[GetExeSuffix]"
        if { [IsDebugBuild] } {
            SingleFileCopy "$instDir/bin/[GetWishName $libVersion]" "$instDir/bin" "wishg[GetExeSuffix]"
            set tkDir "tk[GetMajorMinor $libVersion .]"
            if { [IsWindows] } {
                SingleFileCopy "$instDir/lib/${tkDir}g/pkgIndex.tcl" "$instDir/lib/${tkDir}"
            } elseif { [IsLinux] } {
                if { [file isdirectory "$instDir/lib[GetBits true]/${tkDir}"] } {
                    SingleFileCopy "$instDir/lib[GetBits true]/${tkDir}/pkgIndex.tcl" "$instDir/lib/${tkDir}"
                }
            } else {
                SingleFileCopy "$instDir/lib/${tkDir}/pkgIndex.tcl" "$instDir/lib/${tkDir}"
            }
        }
    }

    if { [UseStage "Distribute" $libName] } {
        StripLibraries "$instDir"
        if { [IsWindows] } {
            MultiFileCopy "$instDir/bin"  "$devDir/[GetTclBinDir]"   "*.exe *.dll"
            MultiFileCopy "$instDir/bin"  "$distDir/[GetTclBinDir]"  "*.exe *.dll"
            if { [UseWinCompiler $libName "vs"] || [NeedDll2Lib $libName] } {
                MultiFileCopy "$instDir/bin"  "$devDir/[GetTclLibDir]"   "*.lib"
                MultiFileCopy "$instDir/bin"  "$distDir/[GetTclLibDir]"  "*.lib"
            }
        } else {
            MultiFileCopy "$instDir/bin"  "$devDir/[GetTclBinDir]"
            MultiFileCopy "$instDir/bin"  "$distDir/[GetTclBinDir]"
        }
        MultiFileCopy "$instDir/include"  "$devDir/[GetTclIncDir]"   "*"  true
        MultiFileCopy "$instDir/include"  "$distDir/[GetTclIncDir]"  "*"  true

        LibFileCopy "$instDir"  "$devDir/[GetTclDir]"   "*"  true
        LibFileCopy "$instDir"  "$distDir/[GetTclDir]"  "*"  true
    }
    return true
}
