# Copyright: 2016-2021 Paul Obermeier (obermeier@tcl3d.org)
# Distributed under BSD license.
#
# BuildType: MSys / gcc
#
# Changes to source code (see ReplaceLine below):
# win/configure: Remove deletion of directory pkgs.
# Otherwise sqlite and Co are not built.

proc Init_Tcl { libName libVersion } {
    SetScriptAuthor    $libName "Paul Obermeier" "obermeier@tcl3d.org"
    SetLibHomepage     $libName "http://www.tcl-lang.org/"
    SetLibDependencies $libName "None"
    SetPlatforms       $libName "All"
    SetWinCompilers    $libName "gcc"
}

proc Env_Tcl { libName libVersion buildDir instDir devDir distDir } {
    SetEnvVar "TCLLIBPATH" "$devDir/[GetTclDir]/lib"
    AddToPathEnv "$devDir/opt/$libName/bin"
}

proc Build_Tcl { libName libVersion buildDir instDir devDir distDir } {
    set buildDirMSys [MSysPath $buildDir]
    set instDirMSys  [MSysPath $instDir]

    if { [UseStage "Extract" $libName] } {
        ExtractLibrary $libName $buildDir
        ReplaceLine "$buildDir/win/configure" \
                    "rm -Rf pkgs" \
                    "# rm -Rf pkgs"

        # The following replacements are needed for Build script TclTkManual.bawt.
        set scriptFile "$buildDir/tools/tcltk-man2html.tcl"
        ReplaceKeywords "$scriptFile" [list "tcl\$useversion" "Tcl\$useversion"]
        ReplaceKeywords "$scriptFile" [list "tk\$useversion" "Tk\$useversion"]

        if { [GetMajor [GetLibVersion "Tcl"]] >= 8 && \
             [GetMinor [GetLibVersion "Tcl"]] >= 7 } {
            SingleFileCopy "$buildDir/libtommath/tommath.h" "$buildDir/generic"
        }
    }

    if { [UseStage "Configure" $libName] } {
        set cmd ""
        if { [IsWindows] && [Is32Bit] } {
            append cmd "CFLAGS=-D_USE_32BIT_TIME_T "
        }
        if { [IsWindows] } {
            append cmd "$buildDirMSys/win/configure "
            append cmd "--build=[GetMingwVersion] "
        } else {
            append cmd "$buildDirMSys/unix/configure "
        }
        append cmd "--prefix=$instDirMSys --exec-prefix=$instDirMSys "
        if { [Is64Bit] } {
            append cmd "--enable-64bit "
        }
        if { [IsDebugBuild] } {
            append cmd "--enable-symbols "
        } else {
            append cmd "--disable-symbols "
        }
        append cmd "--disable-zipfs "

        MSysRun $libName "${libName}_Configure" $buildDir "$cmd"
    }

    if { [UseStage "Compile" $libName] } {
        MSysBuild $libName $buildDir "install"
        # Install internal header files, too. Needed for Togl widget.
        MSysBuild $libName $buildDir "install-private-headers"

        set tclLib [GetTclLibName $libVersion]
        if { [NeedDll2Lib $libName] } {
            Dll2Lib $libName "$instDir/bin" "${tclLib}.dll" "${tclLib}.def" "${tclLib}.lib"
        }

        SingleFileCopy "$instDir/bin/[GetTclshName $libVersion]" "$instDir/bin" "tclsh[GetExeSuffix]"
        if { [IsDebugBuild] } {
            SingleFileCopy "$instDir/bin/[GetTclshName $libVersion]" "$instDir/bin" "tclshg[GetExeSuffix]"
        }
    }

    if { [UseStage "Distribute" $libName] } {
        StripLibraries "$instDir"
        if { [IsWindows] } {
            MultiFileCopy "$instDir/bin"  "$devDir/[GetTclBinDir]"   "*.exe *.dll"
            MultiFileCopy "$instDir/bin"  "$distDir/[GetTclBinDir]"  "*.exe *.dll"
            if { [UseWinCompiler $libName "vs"] || [NeedDll2Lib $libName] } {
                MultiFileCopy "$instDir/bin"  "$devDir/[GetTclLibDir]"   "*.lib"
                MultiFileCopy "$instDir/bin"  "$distDir/[GetTclLibDir]"  "*.lib"
            }
        } else {
            MultiFileCopy "$instDir/bin"  "$devDir/[GetTclBinDir]"
            MultiFileCopy "$instDir/bin"  "$distDir/[GetTclBinDir]"
        }

        MultiFileCopy "$instDir/include"  "$devDir/[GetTclIncDir]"   "*"  true
        MultiFileCopy "$instDir/include"  "$distDir/[GetTclIncDir]"  "*"  true

        LibFileCopy "$instDir"  "$devDir/[GetTclDir]"   "*"  true
        LibFileCopy "$instDir"  "$distDir/[GetTclDir]"  "*"  true
    }
    return true
}
